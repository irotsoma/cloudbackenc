buildscript {
    ext.springBootVersion = "1.3.5.RELEASE"
    ext.kotlinVersion = "1.0.3"
    ext.localMavenRepository = "$rootDir/../irotsoma-maven-repository"
    repositories {
        mavenCentral()
        maven { url "http://repo.spring.io/plugins-release" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "org.springframework.build.gradle:propdeps-plugin:0.0.7"
    }
}

allprojects {
    group "com.irotsoma.cloudbackenc"
    version "0.1-SNAPSHOT"
    apply plugin: 'java'
    apply plugin: 'kotlin'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"
        testCompile group: "junit", name: "junit", version: "4.11"
    }
}


project(":common"){
    dependencies {
        compile "org.apache.logging.log4j:log4j-1.2-api:2.6.2"
        compile "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"
    }

    apply plugin: "maven-publish"
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
        repositories {
            maven {
                url localMavenRepository
            }
        }
    }
}

project(":cloudservice"){
    apply plugin: "maven-publish"

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
        repositories {
            maven {
                url localMavenRepository
            }
        }
    }
}

project(":centralcontroller"){
    apply plugin: "spring-boot"
    apply plugin: "propdeps"

    dependencies {
        //spring deps
        compile "org.springframework.boot:spring-boot-starter-actuator"
        compile "org.springframework.boot:spring-boot-starter-web"
        compile "org.springframework.boot:spring-boot-starter-logging"
        compile "org.springframework:spring-context"
        optional "org.springframework.boot:spring-boot-configuration-processor"
        //cloud service interfaces
        compile project(":cloudservice")
        compile project(":common")
        //jackson
        compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.8.0"
        compile "com.fasterxml.jackson.core:jackson-core:2.8.0"
        compile "com.fasterxml.jackson.core:jackson-databind:2.8.0"
        compile "com.fasterxml.jackson.core:jackson-annotations:2.8.0"
        //kotlin lib
        compile "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"
        //testing deps
        testCompile "org.springframework.boot:spring-boot-starter-test"

    }
    compileJava.dependsOn(processResources)

    processResources {
        expand(project.properties)
    }

    springBoot {
        executable = true
    }

    apply plugin: "maven-publish"
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
        repositories {
            maven {
                // change to point to your repo, e.g. http://my.org/repo
                url localMavenRepository
            }
        }
    }

}